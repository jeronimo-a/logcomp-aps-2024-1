roof         = "   _!_";
story        = "  |o o|";
ground_floor = "__|_#_|__";

INPUT = roof, "\n", story, ["$"], "\n", {story, ["$"], "\n"}, ground_floor, ["$"], "\n";

BLOCK = INPUT, {STATEMENT};
STATEMENT = ((ASSIGN_STAT | PRINT_STAT | WHILE_STAT | IF_STAT | DECLARE_STAT | CALL_STAT | ENTER_STAT | LEAVE_STAT | PUSH_STAT | UP_STAT | DOWN_STAT | LITERAL_PRINT_STAT), ("\n" | EOF)) | "\n";

ASSIGN_STAT          = "pombo", ident, "agora", "is", B_EXPRESSION;
PRINT_STAT           = "where", ("estou" | "está"), "?", "?", "?";
WHILE_STAT           = "enquanto", "waiting", "elevador", "\n", {STATEMENT}, "end";
IF_STAT              = "térreo", "?", "?", "?", "\n", {STATEMENT}, "end"; 
DECLARE_STAT         = "grab", "pombo", ident, ["é", B_EXPRESSION];
CALL_STAT            = "call", "elevador";
ENTER_STAT           = "enter", "elevador";
LEAVE_STAT           = "leave", "elevador";
PUSH_STAT            = "push", "botão", "for", "o", story_number, "piso";
UP_STAT              = "subir", "stairs";
DOWN_STAT            = "descer", "stairs";
LITERAL_PRINT_STAT   = "show", B_EXPRESSION;

B_EXPRESSION = B_TERM, {"or", B_TERM};
B_TERM       = R_EXPRESSION, {"and", R_EXPRESSION};
R_EXPRESSION = EXPRESSION, {("==" | ">" | "<" | "!=" | ">=" | "<="), EXPRESSION};
EXPRESSION   = TERM, {("+" | "-" ), TERM};
TERM         = FACTOR, {("/" | "*"), FACTOR};

FACTOR = num | ident | ("+" | "-" | "not", FACTOR) | ("(", B_EXPRESSION, ")") | ("where", ("estou", "está"));

story_number = ("ground" | num);

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" | "Á" | "É" | "Í" | "Ó"
       | "Ú" | "á" | "é" | "í" | "ó" | "ú" | "À"
       | "à" | "Â" | "Ê" | "Ô" | "â" | "ê" | "ô"
       | "Ã" | "Õ" | "ã" | "õ" | "Ç" | "ç";

symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | '"' | "=" | "|" | "." | "," | ";" | "-" 
       | "+" | "*" | "?" | "\n" | "\t" | "\r" | "\f" | "\b" ;

num   = digit, {digit};
ident = letter, {digit | letter | "_"};
